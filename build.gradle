import java.time.Instant

plugins {
	id 'java'
	id 'maven-publish'
}

def ENV = System.getenv()
version = "${project_version}-${ENV.GITHUB_RUN_NUMBER ? 'build.' + ENV.GITHUB_RUN_NUMBER : 'local.' + Instant.now().epochSecond}"
group = 'dev.latvian.apps'
base.archivesName = 'json'

println("Building version: ${version}")

repositories {
	mavenCentral()

	maven {
		url "https://maven.latvian.dev/releases"
		content {
			includeGroup "dev.latvian.apps"
		}
	}
}

dependencies {
	compileOnly('dev.latvian.apps:tiny-java-server:1.0.0-build.33')
	compileOnly('org.jetbrains:annotations:24.0.1')

	testImplementation('dev.latvian.apps:ansi:1.0.0-build.11')
	testImplementation('org.junit.jupiter:junit-jupiter:6.0.1')
	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

jar {
	manifest {
		attributes 'Implementation-Version': archiveVersion
	}
}

test {
	useJUnitPlatform()
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(21)
	options.compilerArgs << '-parameters' << '-Xmaxerrs' << '1000'
}

java {
	sourceCompatibility = targetCompatibility = '21'
	withSourcesJar()
	withJavadocJar()
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
	publications {
		mavenIchor(MavenPublication) {
			artifactId = 'json'
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_URL && ENV.MAVEN_USERNAME && ENV.MAVEN_TOKEN) {
			maven {
				url = ENV.MAVEN_URL
				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_TOKEN
				}
			}
		}
	}
}